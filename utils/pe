#!/usr/bin/perl
#
#

$VERSION = '0.7';

use strict;
use warnings;
use Getopt::Long qw(:config pass_through);
use Pod::Usage;
use File::Basename;

my $verbose = 0;

GetOptions(
    'verbose'    => \$verbose,
    'usage'      => sub { pod2usage(-verbose => 0, -exitval => 0) }, 
    'help|?'     => sub { pod2usage(-verbose => 1, -exitval => 0) }, 
    'man'        => sub { pod2usage(-verbose => 2, -exitval => 0) }, 
    'version|V'  => sub { print basename($0), " v$main::VERSION\n";  exit 0}, 
);

my %path;
my ($high, $low) = (0,0,0);
for (split /:/, $ENV{PATH} ){
    next if exists $path{$_};     # remove duplicates
    $path{$_} = $high;
    ++$high;
}

for (@ARGV){
    /^-(.*)/ and do { delete $path{$1} if exists $path{$1}; next;};
    /^\+/    and do { add_dir($_); next; };
    print STDERR "Do not know what to do with \"$_\"\n";
}

if ( defined $path{'.'} ){
    warn "You have \".\" in your path!  We will sort it last, please consider removing it!\n";
    ++$high;
    $path{'.'} = $high;
}


# "." shouldn't ever be n your path but if it is make it late.
print join ":", sort { $path{$a} <=> $path{$b} } keys %path;
print "\n";
exit;

sub add_dir {
    my $dir = $_[0] || die "Did not get passed a dirname\n";
    my ($end, $msg);
    if ($dir =~ s/^\+\+//){
        --$low;
        $end = $low;
        $msg = "$dir will be prepended to path\n";
    }
    else {
        $dir =~ s/^\+//;
        ++$high;
        $end = $high;
        $msg = "$dir will be appended to path\n";
    }

    if (exists $path{$dir}){
        print STDERR "$dir is already in path\n";
        delete $path{$dir};
    }

    if ( ! -d $dir ){
        print STDERR "No such directory $dir\n";
        return;
    }
    print STDERR $msg if $verbose;
    $path{$dir} = $end;
}

=head1 NAME

    pe

=head1 SYNOPSIS

    pe +/some/path -/some/other/path

=head1 DESCRIPTION

 Add or remove items from path

 use it like this 
    PATH=`pe -/some/path +/some/other/path`
        + apends to path
        ++ prepends to path
        - removes from path

 or better still like this-
    PATH=`pe +/your/additional/path || echo $PATH`
 so that if for some reason we die $PATH will retain it's original value...

=head1 OPTIONS

 --verbose
    Be noisy about what we are doing...
 --help
    Print this message.
 --man
    Print the man page.
 --version|-V
    Print the program version number.

=head1 BUGS

None that I know of :-)

=head1 AUTHOR

    Murray Barton
    email: murray.barton at gmail.com
    http://incommunique.blogspot.com

=head1 COPYRIGHT

Copyright (c) 2003. Murray Barton. All rights reserved.

This program is free software; you can redistribute it and/or
modify it under the same terms as Perl itself.

See http://www.perl.com/perl/misc/Artistic.html

=head1 DISCLAIMER

 This program is distributed in the hope that it will be useful,
 but WITHOUT ANY WARRANTY; without even the implied warranty
 of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.

=cut

